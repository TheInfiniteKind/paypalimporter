import java.text.SimpleDateFormat
import proguard.gradle.ProGuardTask

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile(':moneydance:1358')
    compile(':prompt:')
    compile(':paypal-core-paypalimporter:1.7.0')
    compile('com.paypal.sdk:merchantsdk:2.14.117') {
        exclude group: 'com.paypal.sdk', module: 'paypal-core'
    }
    compile('net.jcip:jcip-annotations:1.0')
    compile(':jgoodies-common:1.9.0')
    compile(':jgoodies-forms:1.10')
    compile('com.jgoodies:jgoodies-validation:2.5.1') {
        exclude group: 'com.jgoodies', module: 'jgoodies-common'
    }
    compile('com.brsanthu:google-analytics-java:1.1.2')
    compile('org.apache.commons:commons-lang3:3.4')
    compile('commons-configuration:commons-configuration:1.10')
    compile('org.slf4j:slf4j-api:1.7.13')
    compile('org.checkerframework:checker:1.9.11')
}

jar {
    baseName rootProject.name
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                   'Built-Date': getCurrentTimestamp()
    }
}

assemble.dependsOn shadowJar
build.dependsOn shadowJar

shadowJar {
    baseName rootProject.name
    from 'NOTICE'
    from 'LICENSE'
    exclude 'META-INF/**/*'

    dependencies {
        include dependency('commons-lang:commons-lang:2.6')
        include dependency('org.apache.commons:commons-lang3:3.4')
        include dependency('commons-configuration:commons-configuration:1.10')
        include dependency('org.apache.httpcomponents:httpcore:4.3')
        include dependency('org.apache.httpcomponents:httpclient:4.3')
        include dependency('net.jcip:jcip-annotations:1.0')
        include dependency(':jgoodies-common:1.9.0')
        include dependency(':jgoodies-forms:1.10')
        include dependency('com.jgoodies:jgoodies-validation:2.5.1')
        include dependency('commons-logging:commons-logging:1.1.1')
        include dependency('com.brsanthu:google-analytics-java:1.1.2')
        include dependency('com.paypal.sdk:merchantsdk:2.14.117')
        include dependency(':paypal-core-paypalimporter:1.7.0')
        include dependency(':prompt:')
        include dependency('org.slf4j:slf4j-api:1.7.13')
        include dependency('org.slf4j:slf4j-nop:1.7.13')
    }
    relocate 'com.jgoodies',    'com.moneydance.modules.features.paypalimporter.jgoodies'
    relocate 'org.apache.http', 'com.moneydance.modules.features.paypalimporter.apache.http'
}

task proguard(type: ProGuardTask, dependsOn: 'shadowJar') {
    injars  shadowJar.archivePath
    outjars "${project.distsDir}/proguard-${rootProject.name}.jar"
    configuration 'config/proguard.cfg'
}

proguard.doLast {
    new File("${project.distsDir}/proguard-${rootProject.name}.jar").renameTo mxtfile as String
}

shadowJar.finalizedBy proguard

javadoc {
    options.author = true
    options.version = true
    options.memberLevel = JavadocMemberLevel.PACKAGE
    options.use = true
    options.linkSource = true
    options.docTitle = 'PayPal Importer for Moneydance'
    options.windowTitle = 'PayPal Importer for Moneydance'
    options.links = [
            'http://download.oracle.com/javase/7/docs/api/',
            'http://moneydance.com/dev/apidoc/',
            'https://brsanthu.github.io/google-analytics-java/javadocs/',
            'http://www.slf4j.org/apidocs/',
            'https://commons.apache.org/proper/commons-configuration/javadocs/v1.10/apidocs/',
            'https://commons.apache.org/proper/commons-lang/javadocs/api-3.4/',
            'https://commons.apache.org/proper/commons-lang/javadocs/api-2.6/',
            'http://commons.apache.org/logging/apidocs/'
    ]
    options.footer = '<a href="https://github.com/my-flow/paypalimporter">PayPal Importer on GitHub</a>'
}

def String getCurrentTimestamp() {
  Date today = new Date()
  SimpleDateFormat df = new SimpleDateFormat('yyyy-MM-dd HH:mm:ss')
  return df.format(today)
}
