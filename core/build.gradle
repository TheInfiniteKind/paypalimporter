import java.util.jar.Attributes

import java.text.SimpleDateFormat

import org.apache.commons.configuration.Configuration;
import org.apache.commons.configuration.PropertiesConfiguration

import proguard.gradle.ProGuardTask


apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile(':moneydance:1358')
    compile(':prompt:')
    compile(':paypal-core-paypalimporter:1.7.2')
    compile('com.paypal.sdk:merchantsdk:2.15.122') {
        exclude group: 'com.paypal.sdk', module: 'paypal-core'
    }
    compile('net.jcip:jcip-annotations:1.0')
    compile(':jgoodies-common:1.9.0')
    compile(':jgoodies-forms:1.10')
    compile('com.jgoodies:jgoodies-validation:2.5.1') {
        exclude group: 'com.jgoodies', module: 'jgoodies-common'
    }
    compile('org.apache.commons:commons-lang3:3.7')
    compile('commons-configuration:commons-configuration:1.10')
    compile('org.slf4j:slf4j-api:1.7.25')
    compile('org.checkerframework:checker:2.3.2')
}

jar {
    baseName rootProject.name
    manifest {
        attributes ((Attributes.Name.IMPLEMENTATION_TITLE.toString()): getConfigValueForKey("module_name"),
                    (Attributes.Name.IMPLEMENTATION_VERSION.toString()): getConfigValueForKey("module_build"),
                    (Attributes.Name.IMPLEMENTATION_VENDOR.toString()): getConfigValueForKey("vendor"),
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': getCurrentTimestamp(),
                    'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')

    }
}

assemble.dependsOn shadowJar
build.dependsOn shadowJar

shadowJar {
    baseName rootProject.name
    from 'NOTICE'
    from 'LICENSE'
    exclude 'META-INF/**/*'

    dependencies {
        include dependency('commons-lang:commons-lang:2.6')
        include dependency('org.apache.commons:commons-lang3:3.7')
        include dependency('commons-configuration:commons-configuration:1.10')
        include dependency('net.jcip:jcip-annotations:1.0')
        include dependency(':jgoodies-common:1.9.0')
        include dependency(':jgoodies-forms:1.10')
        include dependency('com.jgoodies:jgoodies-validation:2.5.1')
        include dependency('commons-logging:commons-logging:1.2')
        include dependency('com.paypal.sdk:merchantsdk:2.15.122')
        include dependency(':paypal-core-paypalimporter:1.7.2')
        include dependency(':prompt:')
        include dependency('org.slf4j:slf4j-api:1.7.25')
        include dependency('org.slf4j:slf4j-nop:1.7.25')
    }
    relocate 'com.jgoodies', 'com.moneydance.modules.features.paypalimporter.jgoodies'
}

task proguard(type: ProGuardTask, dependsOn: 'shadowJar') {
    injars  shadowJar.archivePath
    outjars "${project.distsDir}/proguard-${rootProject.name}.jar"
    configuration 'config/proguard.cfg'
}

proguard.doLast {
    new File("${project.distsDir}/proguard-${rootProject.name}.jar").renameTo mxtfile as String
}

shadowJar.finalizedBy proguard

javadoc {
    options.author = true
    options.version = true
    options.memberLevel = JavadocMemberLevel.PACKAGE
    options.use = true
    options.linkSource = true
    options.docTitle = 'PayPal Importer for Moneydance'
    options.windowTitle = 'PayPal Importer for Moneydance'
    options.links = [
            'http://download.oracle.com/javase/8/docs/api/',
            'http://moneydance.com/dev/apidoc/',
            'http://www.slf4j.org/apidocs/',
            'https://commons.apache.org/proper/commons-configuration/javadocs/v1.10/apidocs/',
            'https://commons.apache.org/proper/commons-lang/javadocs/api-3.7/',
            'https://commons.apache.org/proper/commons-lang/javadocs/api-2.6/',
            'http://commons.apache.org/logging/apidocs/'
    ]
    options.footer = '<a href="https://github.com/my-flow/paypalimporter">PayPal Importer on GitHub</a>'
}

String getCurrentTimestamp() {
  Date today = new Date()
  SimpleDateFormat df = new SimpleDateFormat('yyyy-MM-dd HH:mm:ss')
  return df.format(today)
}

String getConfigValueForKey(String key) {
    file('src/main/resources/com/moneydance/modules/features/paypalimporter/meta_info.dict').withInputStream {
        final Configuration config = new PropertiesConfiguration();
        config.load(it);
        return config.getString("\"" + key + "\"").replaceAll("^\"|\"\$", "");
    }
}
